import com.moowork.gradle.grunt.GruntTask
import com.moowork.gradle.node.task.NodeTask

buildscript {
  repositories { jcenter() }
  dependencies {
    classpath 'org.grails:grails-gradle-plugin:2.0.1'
    classpath 'com.moowork.gradle:gradle-grunt-plugin:0.5'
  }
}

apply plugin: 'grails'
apply plugin: 'grunt'

repositories {
  grails.central()
}

grails {
  grailsVersion = '2.4.0'
  groovyVersion = '2.3.1'
  springLoadedVersion = '1.1.3'
}

dependencies {
  bootstrap 'org.grails.plugins:tomcat:7.0.52.1'

  compile 'org.grails.plugins:asset-pipeline:1.8.7'

  runtime 'com.h2database:h2:1.4.178'
}

project.afterEvaluate {
  project.tasks.withType(GruntTask) { task ->
    task.dependsOn npmInstall
  }
}
task compileJs(type: GruntTask) {
  args = ['compile']
}

assemble.dependsOn compileJs

project.tasks.'grails-war'.mustRunAfter compileJs

class BowerTask extends NodeTask {
  private String bowerScriptPath

  BowerTask() {
    group = 'Bower'
  }

  String getBowerScript() {
    bowerScriptPath ?: 'node_modules/bower/bin/bower'
  }

  @Override
  void exec() {
    def localBower = project.file(bowerScript)
    if (!localBower.file) {
      throw new GradleException('Bower not installed')
    }
    setScript(localBower)
    super.exec()
  }
}

project.afterEvaluate {
  project.tasks.withType(BowerTask) { task ->
    task.dependsOn npmInstall
  }
}

task bowerInstall(type: BowerTask) {
  args = ['install']
  inputs.file 'bower.json'
  inputs.file '.bowerrc'
  outputs.dir 'grails-app/assets/javascripts'
}

assemble.dependsOn bowerInstall

project.tasks.'grails-war'.mustRunAfter bowerInstall

project.tasks.'grails-run-app'.dependsOn bowerInstall, compileJs

clean {
  delete file('grails-app/assets/javascripts')
}
