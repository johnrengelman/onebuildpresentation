import groovy.io.FileType

def skippedDirs = ['buildSrc', 'gradle']
def path = [] as LinkedList
rootDir.traverse(
  type: FileType.FILES,
  nameFilter: ~/.*\.gradle/, //find .gradle files
  maxDepth: 3, //limit search depth
  preDir: {
      path << it.name // build up the directory structure
      if (skippedDirs.contains(it.name)) { // ignore skipped directories
          return FileVisitResult.SKIP_SUBTREE
      }
  },
  postDir: { path.removeLast() } //drop the .gradle file off the path
) {
    if (path) {
        include path.join(':') //register the project path using : notation
    }
}

rootProject.children.each { project ->
    setSubprojectBuildFile(project)
}

void setSubprojectBuildFile(def project) {
    String fileBaseName = project.name.replaceAll("\\p{Upper}") { "-${it.toLowerCase()}"}
    project.buildFileName = "${fileBaseName}.gradle"
    assert project.buildFile.isFile()
    project.children.each { subproject ->
        setSubprojectBuildFile(subproject)
    }
}
